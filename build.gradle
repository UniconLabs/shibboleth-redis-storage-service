import groovy.swing.SwingBuilder

plugins {
    id 'java'
    id 'idea'
    id 'distribution'
    id 'com.avast.gradle.docker-compose' version '0.8.0'
    id 'net.researchgate.release' version '2.7.0'
    id 'signing'
}

apply from: 'gradle/shibboleth.gradle'

repositories {
    maven { url 'https://build.shibboleth.net/nexus/content/groups/public' }
    jcenter()
    mavenCentral()
}

dependencies {
    compileOnly "net.shibboleth.idp:idp-admin-api:${project.'shibboleth.version'}"
    compileOnly "net.shibboleth.idp:idp-admin-impl:${project.'shibboleth.version'}"
    compileOnly "org.opensaml:opensaml-storage-api:${project.'opensaml.version'}"
    compileOnly "ch.qos.logback:logback-core:${project.'logback.version'}"

    implementation "commons-configuration:commons-configuration:${project.'commons-configuration.version'}", {
        exclude module: 'commons-lang'
    }
    implementation "org.redisson:redisson:${project.'redisson.version'}", {
        exclude module: 'jackson-annotations'
        exclude module: 'jackson-core'
        exclude module: 'jackson-databind'
        exclude module: 'javassist'
        exclude module: 'slf4j-api'
        exclude module: 'byte-buddy'
    }

    testImplementation "net.shibboleth.idp:idp-authn-api:${project.'shibboleth.version'}"
    testImplementation "org.opensaml:opensaml-storage-api:${project.'opensaml.version'}:tests@jar"
    testImplementation "junit:junit:${project.'junit.version'}"
    testImplementation "org.testng:testng:${project.'testng.version'}"
    testImplementation "com.fasterxml.jackson.core:jackson-core:${project.'jackson.version'}"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:${project.'jackson.version'}"
}

processResources {
    from ('src/main/java') {
        include "**/*.properties"
    }
}

clean {
    delete (layout.projectDirectory.dir("src/dist/bootstrap/plugin.properties"))
    delete fileTree(layout.projectDirectory.dir("src/dist/webapp/WEB-INF/lib")) {
        include "**/*.jar"
    }
}

jar {
    manifest {
        attributes "Name": "net/unicon/iam/shibboleth/storage/redis/plugin/",
                   "Implementation-Vendor": "unicon.net",
                   "Implementation-Version": "${project.version}",
                   "Implementation-Title": "${project.name}"

    }
}
jar.finalizedBy("buildPluginArtifacts")

task buildPluginArtifacts(type: GradleBuild, dependsOn: ['clean', 'jar']) {
    group 'Build'
    description 'Build the deployable artifacts for the plugin'

    copy {
        from layout.projectDirectory.dir("src/main/java/net/unicon/iam/shibboleth/storage/redis/plugin/plugin.properties")
        into layout.projectDirectory.dir("src/dist/bootstrap")
    }
    copy {
        from layout.buildDirectory.dir("libs/${project.name}-${project.version}.jar")
        into layout.projectDirectory.dir("src/dist/webapp/WEB-INF/lib")
    }
    copy {
        from { project.configurations.runtimeClasspath }
        into ('src/dist/webapp/WEB-INF/lib')
    }
    finalizedBy distTar {
        archivesBaseName = "${project.name}"
        archiveExtension = 'tar.gz'
        compression = Compression.GZIP

        from layout.projectDirectory.dir("src/dist/")
        exclude "**/.gitkeep"
        exclude "/${project.name}-${project.version}"
        finalizedBy distZip {
            archivesBaseName = "${project.name}"

            from layout.projectDirectory.dir("src/dist/")
            exclude "**/.gitkeep"
            exclude "/${project.name}-${project.version}"
        }
    }
}

signing {
    sign configurations.archives
}

/*
 * Docker Compose (gradle-docker-compose-plugin) settings.
 * Used to start and stop docker containers before running tests.
 */
apply plugin: 'docker-compose'
dockerCompose {
    useComposeFiles = ['./src/test/docker/docker-compose.yml']
    captureContainersOutput = true
    waitForTcpPorts = false
}

test {
    useTestNG()
    scanForTestClasses = false
    include 'net/unicon/iam/shibboleth/storage/redis/**/*'
    dependsOn 'composeUp'
    finalizedBy 'composeDown'
}